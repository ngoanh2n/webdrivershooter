/**
 * Author: ngoanh2n
 */
tasks.register("sourcesJar", Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

tasks.register("javadocJar", Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = property("artifact.name")
                description = property("artifact.desc")
                url = "https://github.com/ngoanh2n/webdrivershooter"

                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                    }
                }
                developers {
                    developer {
                        id = "ngoanh2n"
                        name = "ngoanh2n"
                        email = "ngoanh2n@gmail.com"
                        organizationUrl = "https://github.com/ngoanh2n"
                    }
                }
                scm {
                    url = "https://github.com/ngoanh2n/webdrivershooter"
                    connection = "scm:git:git://github.com/ngoanh2n/webdrivershooter.git"
                    developerConnection = "scm:git:ssh://github.com/ngoanh2n/webdrivershooter.git"
                }
            }
        }
    }
    if (project.hasProperty("signing.keyId")) {
        repositories {
            maven {
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                url project.version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = "${sonatypeUsername}"
                    password = "${sonatypePassword}"
                }
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}
